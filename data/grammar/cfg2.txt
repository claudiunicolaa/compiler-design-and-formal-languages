PROGRAM_DEF         -> PROG_HEADING BLOCK

PROG_HEADING        -> program __IDENTIFIER__ ;

BLOCK               -> DECL_LIST COMPOUND_STMT

DECL_LIST           -> DECL ; DECL_LIST
                     | DECL ;

DECL                -> const __IDENTIFIER__ = __CONSTANT__
                     | declare IDENTIFIER_LIST as TYPE

IDENTIFIER_LIST     -> __IDENTIFIER__
                     | __IDENTIFIER__ , IDENTIFIER_LIST

COMPOUND_STMT       -> { STMT_SEQ }

STMT_SEQ            -> STMT ;
                     | STMT ; STMT_SEQ

STMT                -> SIMPLE_STMT
                     | STRUCTURED_STMT

SIMPLE_STMT         -> ASSIGN_STMT
                     | RETURN_STMT
                     | IO_STMT

ASSIGN_STMT         -> __IDENTIFIER__ = EXPRESSION

RETURN_STMT         -> return EXPRESSION

IO_STMT             -> read ( __IDENTIFIER__ )
                     | write ( __IDENTIFIER__ )

STRUCTURED_STMT     -> IF_STMT
                     | COMPOUND_STMT
                     | WHILE_STMT

WHILE_STMT          -> while ( CONDITION ) STMT

IF_STMT             -> if ( CONDITION ) STMT

CONDITION           -> EXPRESSION RELATIONAL_OPERATOR EXPRESSION

EXPRESSION          -> TERM
                     | TERM OPERATOR TERM

OPERATOR            -> +
                     | -
                     | ||
                     | *
                     | /
                     | %
                     | &&

TERM                -> __IDENTIFIER__
                     | __CONSTANT__
                     | ( EXPRESSION )

RELATIONAL_OPERATOR -> ==
                     | !=
                     | <
                     | <=
                     | >
                     | >=

VARIABLE            -> __IDENTIFIER__
                     | __IDENTIFIER__ INDEX_PART

INDEX_PART          -> [ EXPRESSION ]
                     | [ EXPRESSION ] INDEX_PART

TYPE                -> ARRAY_TYPE
                     | SCALAR_TYPE

SCALAR_TYPE         -> int
                     | char

ARRAY_TYPE          -> SCALAR_TYPE [ __CONSTANT__ ]
