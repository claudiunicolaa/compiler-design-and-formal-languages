PROGRAM_DEF         -> PROG_HEADING BLOCK

PROG_HEADING        -> program IDENTIFIER ;

BLOCK               -> DECL_PART COMPOUND_STMT

DECL_PART           -> OPT_CONST_DEF_PART OPT_VAR_DEC_PART

OPT_CONST_DEF_PART  -> CONST_DEF_PART
                     | __eps__

OPT_VAR_DEC_PART    -> VAR_DEC_PART
                     | __eps__

CONST_DEF_PART      -> const CONST_DEF CONST_DEF_PART
                     | const CONST_DEF

CONST_DEF           -> IDENTIFIER = CONSTANT ;

VAR_DEC_PART        -> declare VAR_DEC ;
                     | declare VAR_DEC ; VAR_DEC_PART

VAR_DEC             -> IDENTIFIER_LIST as TYPE

IDENTIFIER_LIST     -> IDENTIFIER
                     | IDENTIFIER , IDENTIFIER_LIST

COMPOUND_STMT       -> { STMT_SEQ }

STMT_SEQ            -> STMT ;
                     | STMT ; STMT_SEQ

STMT                -> SIMPLE_STMT
                     | STRUCTURED_STMT

SIMPLE_STMT         -> ASSIGN_STMT
                     | RETURN_STMT
                     | IO_STMT

ASSIGN_STMT         -> IDENTIFIER = EXPRESSION

RETURN_STMT         -> return EXPRESSION

IO_STMT             -> read ( IDENTIFIER )
                     | write ( IDENTIFIER )

STRUCTURED_STMT     -> IF_STMT
                     | COMPOUND_STMT
                     | WHILE_STMT

WHILE_STMT          -> while ( CONDITION ) STMT

IF_STMT             -> IF_PART IF_OPTIONAL_ELSE

IF_PART             -> if ( CONDITION ) STMT

IF_OPTIONAL_ELSE    -> else STMT
                     | __eps__

CONDITION           -> EXPRESSION RELATIONAL_OPERATOR EXPRESSION

EXPRESSION          -> TERM
                     | TERM OPERATOR TERM

OPERATOR            -> +
                     | -
                     | ||
                     | *
                     | /
                     | %
                     | &&

TERM                -> IDENTIFIER
                     | LITERAL_NUMBER
                     | ( EXPRESSION )

RELATIONAL_OPERATOR -> ==
                     | !=
                     | <
                     | <=
                     | >
                     | >=

VARIABLE            -> IDENTIFIER
                     | INDEXED_VARIABLE

INDEXED_VARIABLE    -> IDENTIFIER INDEX_PART

INDEX_PART          -> [ VARIABLE ]
                     | [ VARIABLE ] INDEX_PART
                     | [ LITERAL_NUMBER ]
                     | [ LITERAL_NUMBER ] INDEX_PART

TYPE                -> ARRAY_TYPE
                     | SCALAR_TYPE

SCALAR_TYPE         -> int
                     | char

ARRAY_TYPE          -> SCALAR_TYPE [ LITERAL_NUMBER ]
